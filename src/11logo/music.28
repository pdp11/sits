	.STITL	MUSIC PRIMITIVES
	.PRINT /BEGIN MUSIC FILE
/
	VERN==VERN+%FNAM2

SING:	EXCH (P),2(P)
	JSR	PC,MCHK
	JSR	PC,REVS	;FOR MULTIPLE ARGUMENTS
	MOV	(SP)+,D	;NUMBER OF ARGS
	ASR	D	;IS IT EVEN?
	BCC	SING2
	ERROR+WNA	;WRONG NUMBER OF ARGS
SING2:	MOV	VOICEN,E	;INDEX FOR VOICE (0,2,4 OR 6)
	MOV	VLAST(E),F	;POINTER TO LAST NODE IN VOICE
SING3:	DEC	D
	BGE	SING31
	JMP	NORT	;DONE
SING31:	JSR	PC,G2NARG	;DURATION IN B, PITCH IN A
	ADD	#74,A	;NORMALIZE 0 TO MIDDLE C
	CMP	A,#MBREST	;SMALLEST VALID PITCH
	BGE	SING5
SING4:	ERROR+NOG	;NOTE OUT OF RANGE
SING5:	CMP	A,#137	;HIGHEST VALID NOTE
	BGT	SING4
	TST	B	;DURATION
	BGE	SING7
SING6:	ERROR+DOR	;DURATION OUT OF RANGE
SING7:	CMP	B,#177	;LARGEST LEGAL DURATION (+- 7 BITS)
	BGT	SING6
	ADD	B,VOICLN(E)	;ACCUMULATE TOTAL FOR VOICE
	CMP	B,#1
	BLT	SING3	;IF ZERO THEN GO ON
	BNE	SING8
	NEG	B	;IF 1 THEN SET TO -1 AS FLAG FOR PERFORM
SING8:	SWAB	A
	BIC	#177400,B	;CLEAR TOP HALF
	BIS	A,B	;SET UP 8 BITS PITCH, 8 BITS DURATION IN B
	JSR	PC,ACTSTO	;BUILD ON TO VOICE LIST
	MOV	F,VLAST(E)	;NEW LAST NOTE
	BR	SING3
NOMUSIC:	SPUSH	#NORT
NOMU1:	MOV	#-1,MUSER
	RTS	PC

			;TRY TO GET MUSIC BOX AND INITIALIZE
			;IF DON'T ALREADY HAVE IT
MCHK:	MOV	#MUSER,A
	CMP	(A),USER
	BNE	MCHK1
	RTS	PC
MCHK1:	CMP	(A),#-1
	BEQ	MUSIN	;INITIALIZE
	ERROR+SMB	;SOMEONE ELSE IS USING THE MUSIC BOX
MUSIN:	MOV	USER,(A)+	;CLEAR OUT THE MUSIC VARIABLES IN SYSTEM SPACE
MUSIN1:	CLR	(A)+
	CMP	A,#MUSEND
	BLT	MUSIN1
	MOV	#MVOC,A	;CLEAR OUT THE POINTERS IN USER SPACE
	CLR	(A)+
	CLR	(A)+
	CLR	(A)+
	CLR	(A)+
	JSR	PC,MCL	;SET UP DUMMY NODES
	MOV	#6,NVOIC	;NUMBER OF VOICES
	RTS	PC
			;SET UP DUMMY MUSIC LISTS
MCL:	MOV	#6,F
	CLR	B
	MOV	#LSTR,A
MCL2:	MOV	MVOC(F),TOPS	;FREE MUSIC NODES
	JSR	PC,FRELST
	JSR	PC,GRBAD	;GET A NEW NODE
	BIS	A,C
	CLR	B
	JSR	PC,.STORE
	MOV	C,MVOC(F)	;POINTER TO IT
	MOV	C,VLAST(F)
	CLR	VOICLN(F)
	SUB	#2,F
	BGE	MCL2
	RTS	PC

MCLEAR:	JSR	PC,MCHK
	JSR	PC,MCL
	JMP	NORT
VLEN:	JSR	PC,MCHK	;OUTPUT LENGTH OF CURENT VOICE
	MOV	VOICEN,B
	MOV	VOICLN(B),B
	JMP	R1NARG

MLEN:	JSR	PC,MCHK
	MOV	#4,D
	MOV	#VOICLN,F
	CLR	B
MLEN1:	MOV	(F)+,A	;GET MAXIMUM VOICE LENGTH
	CMP	A,B
	BLE	MLEN2
	MOV	A,B
MLEN2:	DEC	D
	BNE	MLEN1
	JMP	R1NARG
GTVARG:	JSR	PC,G1NARG
	DEC	B	;TRANSFORM VOICE NUMBER (1,2,3,4) TO INDEX (0,2,4,6)
	ASL	B
	TST	B
	BGE	GTV2
GTV1:	ERROR+IVV	;INVALID VOICE NUMBER
GTV2:	CMP	B,#6
	BGT	GTV1
	RTS	PC

VOICE:	JSR	PC,MCHK
	JSR	PC,GTVARG
	MOV	B,VOICEN
	JMP	NORT

NVOICES:	JSR	PC,MCHK
	JSR	PC,MCL	;REINITIALIZE
	JSR	PC,GTVARG
	CMP B,#4
	BNE .+4
	ERROR+IVV	;3-VOICE MODE ILLEGAL FOR NOW (FOREVER?)
	MOV	B,NVOIC	;SET NEW VOICE NUMBER
	JMP	NORT

	MBTRAP=43	;TRAP CHARACTER
	MBFCH=1		;SHUT-UP CHARACTER
	MBREST=40	;REST CHARACTER FOR MUSIC BOX
	MBPERC=42	;HIGHEST CODED PERCUSSION EFFECT

MBVCH:	.BYTE	23	;CTL CHARACTERS FOR NUMBER OF VOICES
	.BYTE	42
	.BYTE	0
	.BYTE	60

MBON:	MOV #MBDN,E
MBSET:	MOV #MBTRAP,D
	JSR PC,TBTYO
	MOV	NVOIC,C	;SET # OF VOICES
	ASR	C
	MOVB	MBVCH(C),D
	JSR	PC,TBTYO
	RTS	PC
	MBSYNC==20.	;SYNCHRONIZE MBOX EVERY THIS MANY CHARACTERS

			;OUTPUT MUSIC TO THE BOX
PM:	JSR	PC,MCHK
	JSR	PC,MBON
	MOV	#MBSYNC,TEMP
	MOV	NVOIC,A
	MOV	A,F	;USED AS POINTER TO CORRECT VOICE
	ADD	#VOICLN+2,A	;SET UP FLAGS IN VOICLN SLOTS
PM1:	MOV	#-1,-(A)
	CMP	A,#VOICLN
	BGT	PM1
PM2:	JSR	PC,PMCHP	;GRAB DUMMY NODE OF EACH VOICE
	JSR	PC,PMCHP	;NOW SET UP FIRST NODE OF MUSIC
PM21:	TST	-(F)
	TST	F
	BGE	PM2
PM3:	MOV USER,F
	TST BRAKE(F)
	BNE PM6
	CLR	F	;LOOP TO OUTPUT NOTES
PM4:	JSR	PC,PMNXT	;F IS INDEX TO VOICE
	TST	(F)+
	CMP	F,NVOIC	;ONE NOTE FROM EACH VOICE
	BNE	PM42
	TST	TEMP	;TIME TO SYNC THE MBOX?
	BGE	PM41
	JSR	PC,MBSET
	MOV	#MBSYNC,TEMP
PM41:	CMP	F,NVOIC
PM42:	BLE	PM4
	CLR	B
	MOV	NVOIC,A	;ADD FLAGS FOR ALL VOICES
	ADD	#VOICLN+2,A
PM5:	ADD	-(A),B
	CMP	A,#VOICLN
	BGT	PM5
	TST	B	;IF ALL FLAGS CLEARED THEN DONE
	BNE	PM3
PM6:	MOV #MBTRAP,D
	JSR PC,TBTYO
	MOV	#MBFCH,D
	JSR	PC,TBTYO	;SILENCE BOX
	JSR	PC,MCL	;REINIT MUSIC VARIABLES
	JMP	NORT
			;OUTPUT ONE NOTE FROM VOICE(F)
PMNXT:	DEC	TEMP	;NOTE COUNTER
	MOV	#MBREST,D
	TST	VOICLN(F)	;IF FLAG CLEARED OUTPUT REST
	BNE	PMNXT2
	JSR	PC,TBTYO	;OUTPUT REST
	RTS	PC
PMNXT2:	CMPB	VLAST(F),#1	;IF DUR=1 PLAY A REST
	BEQ	PMNX2A
	MOVB	VLAST+1(F),D	;PITCH
PMNX2A:	JSR	PC,TBTYO
	DECB	VLAST(F)	;DECREMENT DURATION
	BGT	PMNXT3
	JSR	PC,PMCHP	;IF DUR<=0 SET UP NEXT NODE
	RTS	PC
PMNXT3:	CMPB	D,#MBPERC	;IS THIS A PERCUSSION SOUND?
	BGT	PMNXT4	;NO
	MOVB	#MBREST,VLAST+1(F)	;YES--CHANGE ALL BUT FIRST TO RESTS
PMNXT4:	RTS	PC

PMCHP:	MOV	MVOC(F),C	;SET UP NEXT NODE SKIP IF SUCCESSFUL
	BIC	#170000,C
	BNE	PMCHP1
	CLR	VOICLN(F)	;CLEAR FLAG TO INDICATE NO NEXT NODE
	RTS	PC
PMCHP1:	JSR	PC,.LOADC
	BIC #170000,A
	BIS #LSTR,A
	MOV	A,MVOC(F)
	MOV	B,VLAST(F)	;PUT PITCH,,DURATION IN VLAST SLOT
	JSR	PC,.FREE	;FREE OLD NODE
	RTS	PC
