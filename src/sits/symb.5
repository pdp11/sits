.MLLIT==1
TITLE SYMBOL TABLE CONVERT
A=2
B=3
C=15
D=16
P=17
DIC==1
DOC==2
TYOC==16
TYIC==17

;ITS STYLE COMMAND LINE SCANNER
;	NON-SKIP RETURN FOR NULL FILE SPEC
;CLOBBERS ACS WITH RECKLESS ABANDON

BREAK=1		;RETURNS WITH CHARACTER THAT BROKE SCAN
DEV=4		;RETURNS DEV,FN1,FN2,SNAME
FN1=5
FN2=6
SNAME=7
AC=10
CHAR=11
ACPTR=12
TEMP=13
LIMBO=14	;SCANNER READ AHEAD CHARACTER
		;CANNOT LEAVE NAME UNTIL ZERO
;COMMAND BUFFER FILLING AND GETTING

COMPNT:	0
COMBUF:	BLOCK 69.
COMCNT:	0
CRLF:	.IOT TYOC,[15]
	POPJ P,
TTYLIN:	MOVE B,[010700,,COMBUF-1]
	SETZM COMCNT
TTYL1:	.IOT TYIC,A
	JUMPE A,TTYL1
	CAIN A,177
	JRST RUBOUT
	CAIN A,15
	JRST TTYCR
	CAIN A,14
	JRST TTYL1	;IGNORE FORM FEEDS
	CAIN A,12
	JRST TTYLF
	PUSHJ P,COMPUT
	JRST TTYL1

RUBOUT:	SOSGE COMCNT
	JRST [	PUSHJ P,CRLF
		JRST TTYLIN]
	LDB A,B
	PUSHJ P,TYO
	PUSHJ P,DECBP	;DECREMENT BYTE POINTER
	JRST TTYL1

DECBP:	ADD B,[070000,,0]
	JUMPGE B,CPOPJ
	SUB B,[430000,,1]
	POPJ P,

TTYLF:	MOVEI A,15
	PUSHJ P,TYO
TTYCR:	PUSHJ P,COMPUT
	MOVEI A,12
	PUSHJ P,COMPUT
	MOVE B,[010700,,COMBUF-1]
	MOVEM B,COMPNT
	POPJ P,

COMPUT:	IDPB A,B
	AOS COMCNT
	POPJ P,

COMGET:	SOSGE COMCNT
	.VALUE
	ILDB BREAK,COMPNT
	POPJ P,


GETCC:	0			;GET CHARACTER FOR COMMAND LINE SCANNER
	SKIPN BREAK,LIMBO
	PUSHJ P,COMGET
	SETZM LIMBO
	JRST @GETCC

NAME:	0			;BREAK OFF WORD FROM INPUT STREAM
NA1:	JSR GETCC
	CAIE BREAK,40		;IGNORE LEADING SPACES
	CAIN BREAK,11		;TABS TOO
	JRST NA1
	MOVE ACPTR,[440600,,AC]
	TDZA AC,AC
NAME1:	JSR GETCC
	JSR BRKTST
	JRST NAMBRK		;FOUND A BREAK CHARACTER
NAME2:	TLNE ACPTR,770000	;IGNORE EVERYTHING AFTER 6 CHARACTERS
	IDPB CHAR,ACPTR
	JRST NAME1

NAMBRK:	JUMPN CHAR,@NAME	;NO TRAILING SPACES
NAMBR1:	JSR GETCC
	CAIE BREAK,40		;IGNORE TRAILING SPACES
	CAIN BREAK,11
	JRST NAMBR1
	JSR BRKTST
	JRST @NAME		;A BREAK CHARACTER
	MOVEM BREAK,LIMBO		;SPACE BROKE US
	MOVEI BREAK,40
	JRST @NAME

;CONVERTS BREAK TO SIXBIT AND PUTS RESULT IN CHAR
;^Q QUOTES NEXT CHARACTER
;FAILS TO SKIP ON BREAK CHARACTER

BRKTST:	0
	CAIN BREAK,11
	MOVEI BREAK,40
	JSR SIXTST
	JUMPL CHAR,[	CAIE BREAK,21		;^Q
			JRST @BRKTST		;NON-SIXBIT BREAKS US
			JSR GETCC
			JSR SIXTST
			JUMPL CHAR,@BRKTST	;NON-SIXBIT
			JRST BRKT1]
	JUMPE CHAR,@BRKTST
	CAIE CHAR,':
	CAIN CHAR,';
	JRST @BRKTST
BRKT1:	AOS BRKTST		;WHEW!
	JRST @BRKTST

;CONVERT BREAK TO SIXBIT

SIXTST:	0
	MOVNI CHAR,1
	CAIL BREAK,40
	CAILE BREAK,"_
	JRST SIXT1	;MIGHT BE LOWER CASE
	MOVEI CHAR,-40(BREAK)
	JRST @SIXTST
SIXT1:	CAIL BREAK,"A
	CAILE BREAK,"Z
	JRST @SIXTST
	MOVEI CHAR,<"A-"A-40>(BREAK)
	JRST @SIXTST

;THIS ROUTINE SCANS COMMAND LINE FOR FILE SPECIFICATION

GETFIL:0
	SETZB FN1,FN2
	SETZB DEV,SNAME
	SETZM LIMBO
	SETOM EQUALF'
	JSR GETCC
	CAIE BREAK,"=
	JRST [SETZM EQUALF ? MOVEM BREAK,LIMBO ? JRST .+1]
	JSR NAME
	JUMPE AC,@GETFIL
	AOSA GETFIL
GETF1:	JSR NAME		;BREAK OFF FIRST NAME
	JUMPE AC,@GETFIL	;LET INITL WORRY ABOUT IT
	CAIN BREAK,":
	JRST [	MOVE DEV,AC
		JRST GETF1]
	CAIN BREAK,";
	JRST [	MOVE SNAME,AC
		JRST GETF1]

;THIS MUST BE FN1 OR FN2

	CAIE BREAK,40
	JRST [	JUMPN FN1,[	MOVE FN2,AC
				JRST @GETFIL]
		MOVE FN1,AC
		JRST @GETFIL]
	JUMPN FN1,[	MOVE FN2,AC
			JRST GETF1]
	MOVE FN1,AC
	JRST GETF1

TYO:	.IOT TYOC,A
	POPJ P,
GO:	.OPEN TYOC,[SIXBIT /  !TTY/]
	.VALUE
	.OPEN TYIC,[SIXBIT /   TTY/]
	.VALUE
	MOVEI P,PDL
	PUSHJ P,TTYLIN
	JSR GETFIL
	CAIN DEV,
	MOVSI DEV,'DSK
	HLRM DEV,INFILE
	HLRM DEV,OUTFIL
	CAIN SNAME,
	.SUSET [.RSNAM,,SNAME]
	.SUSET [.SSNAM,,SNAME]
	CAIN FN2,
	MOVE FN2,[SIXBIT /BIN/]
	MOVEM FN1,INFILE+1
	MOVEM FN1,OUTFIL+1
	MOVEM FN2,INFILE+2
	MOVE FN2,[SIXBIT /SYMS/]
	MOVEM FN2,OUTFIL+2
	.OPEN DIC,INFILE
	.VALUE
	.OPEN DOC,OUTFIL
	.VALUE
	.IOT DOC,[".]
	.IOT DOC,["N]
	PUSHJ P,LSTCMD
	PUSHJ P,GETW1
FINDSM:	PUSHJ P,GETW
	TDNN A,[-1,,600000]
	JRST FINDSM
	SETZM POINTF'
	.SUSET [.RJNAM,,B]
	LDB B,[360600,,B]
	CAIN B,".-40
	SETOM POINTF
OUTSYM:	SKIPN POINTF
	JRST OUTSY1
	LDB B,[360600,,A]
	CAIE B,"$-40
	CAIN B,".-40
	JRST OUTSY1
	PUSHJ P,GETW
	JRST OUTSY2
OUTSY1:	.iot doc,["D]
	.IOT DOC,["C]
	.IOT DOC,["S]
	.IOT DOC,[" ]
	PUSHJ P,OUTSIX
;	SKIPE EQUALF
;	.IOT DOC,["=]
;	.IOT DOC,["=]
;	.IOT DOC,["=]
	.IOT DOC,[",]
	PUSHJ P,GETW
	TLNE A,4000
	.IOT DOC,["%]
	PUSHJ P,OUTOCT
	.IOT DOC,[15]
	.IOT DOC,[12]
OUTSY2:	PUSHJ P,GETW
	JUMPN A,OUTSYM
	.CLOSE DIC,
	.IOT DOC,[".]
	PUSHJ P,LSTCMD
	.CLOSE DOC,
	.VALUE [ASCIZ /:KILL /]
GETW:	MOVE A,(TEMP)
	AOBJN TEMP,CPOPJ
GETW1:	MOVE TEMP,[DBLK,,DBLK+1]
	SETZM DBLK
	BLT TEMP,DBLK+1777
	MOVE TEMP,[-2000,,DBLK]
	.IOT DIC,TEMP
	MOVE TEMP,[-2000,,DBLK]
	POPJ P,
DBLK:	BLOCK 2000
PDL:	BLOCK 100
OUTSIX:	MOVE B,[440600,,A]
OUTSX1:	ILDB C,B
	JUMPE C,CPOPJ
	ADDI C,40
	.IOT 2,C
	TLNE B,770000
	JRST OUTSX1
CPOPJ:	POPJ P,
OUTOCT:	MOVE B,[220300,,A]
OUTOC1:	ILDB C,B
	JUMPN C,OUTOC2
	TLNE B,770000
	JRST OUTOC1
	SKIPA
OUTOC3:	ILDB C,B
OUTOC2:	ADDI C,60
	.IOT 2,C
	TLNE B,770000
	JRST OUTOC3
	POPJ P,
INFILE:	SIXBIT /  &DSKL3000 SEGBIN/
LSTCMD:	.IOT DOC,["L]
	.IOT DOC,["I]
	.IOT DOC,["S]
	.IOT DOC,["T]
	.IOT DOC,[15]
	.IOT DOC,[12]
	POPJ P,
OUTFIL:	SIXBIT /  !DSKL3000 SYMBOL/
END GO
